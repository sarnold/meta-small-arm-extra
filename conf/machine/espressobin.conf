#@TYPE: Machine
#@NAME: espressobin (og)

#@DESCRIPTION: Machine configuration for the default espressobin boards

require conf/machine/include/arm/arch-arm64.inc

MACHINEOVERRIDES =. "espressobin:"

MACHINE_ENDIANNESS ?= "le"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-mainline"
PREFERRED_VERSION_linux-mainline ?= "5.10%"

KERNEL_DEVICETREE ?= "marvell/armada-3720-espressobin.dtb"
KERNEL_MODULE_AUTOLOAD += "marvell-cesa ansi_cprng"

PACKAGECONFIG_pn_mesa = "\
    ${@bb.utils.filter('DISTRO_FEATURES', 'wayland ', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'opengl egl gles gbm dri', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'x11 opengl', 'x11', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'x11 vulkan', 'dri3', '', d)} \
    gallium \
"

ROOT_HOME ?= "/root"

KERNEL_IMAGETYPE = "Image"
IMAGE_BOOT_FILES ?= " \
    ${KERNEL_DEVICETREE} \
    ${KERNEL_IMAGETYPE} \
"

IMAGE_FSTYPES ?= "tar.bz2 ext4 wic"
WKS_FILE ?= "sdcard-esp.wks"

WKS_FILE_DEPENDS = " \
    e2fsprogs-native \
    bmap-tools-native \
"

EXTRA_IMAGECMD_ext4 = "-O ^metadata_csum,^64bit"

SERIAL_CONSOLE = "115200 ttyMV0"

MACHINE_FEATURES = "ext2 ipsec nfs pci smbfs usbgadget usbhost vfat"

PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot"
UBOOT_MACHINE ?= "mvebu_espressobin-88f3720_defconfig"

UBOOT_EXTLINUX = "1"
UBOOT_EXTLINUX_ROOT = "root=/dev/mmcblk0p1"
UBOOT_EXTLINUX_FDT = "../armada-3720-espressobin.dtb"
UBOOT_EXTLINUX_CONSOLE = "console=ttyMV0,115200"
UBOOT_EXTLINUX_KERNEL_ARGS = "rootwait rw net.ifnames=0 biosdevname=0"
UBOOT_EXTLINUX_KERNEL_IMAGE = "../Image"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "\
    kernel-devicetree \
    kernel-image \
    kernel-modules \
"

MACHINE_FEATURES_BACKFILL_CONSIDERED = "\
    rtc \
    qemu-usermode \
"
